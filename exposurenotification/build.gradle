/*
 * Copyright 2020 Google LLC
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

apply plugin: 'com.android.library'
apply plugin: 'com.google.protobuf'

/*
 * This library has dependencies upon a few other pieces of code, most notably NanoPB and
 * BoringSSL. In my project setup, I've placed the dependencies off of my home directory. Their
 * actual roots can be anywhere, but the paths should be updated below if they are not kept
 * together under this root.
 * TODO: Update these paths to the dependency roots.
 */
def depsRoot = "${System.properties['user.home']}/Projects"
def boringSslRoot = "${depsRoot}/boringssl"
def nanopbRoot = "${depsRoot}/nanopb-0.4.2-macosx-x86"

android {
    compileSdkVersion 29
    buildToolsVersion "30.0.0"

    compileOptions {
        // Flag to enable support for the new language APIs
        coreLibraryDesugaringEnabled true
        // Sets Java compatibility to Java 8
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 29
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"

        /*
         * BORING_SSL_ROOT: location of the BoringSSL project root from
         * https://boringssl.googlesource.com/boringssl/
         *
         * NANOPB_ROOT: location of NanoPB downloaded from https://jpa.kapsi.fi/nanopb/download/
         */
        externalNativeBuild {
            cmake {
                cppFlags "-std=c++14"
                arguments "-DBORING_SSL_ROOT=${boringSslRoot}",
                        "-DNANOPB_ROOT=${nanopbRoot}"
            }
        }
        ndk {
            abiFilters "arm64-v8a", "x86"
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    externalNativeBuild {
        cmake {
            path "src/main/cpp/CMakeLists.txt"
            version "3.10.2"
        }
    }

    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }
}

protobuf {
    protoc {
        artifact = 'com.google.protobuf:protoc:3.11.4'
    }
    dependencies {
        protobuf files("${depsRoot}/nanopb-0.3.9.2-macosx-x86/generator/proto/")
    }
    generateProtoTasks {
        all().each { task ->
            task.builtins {
                java {
                }
            }
        }
    }
}

dependencies {
    implementation fileTree(dir: "libs", include: ["*.jar"])

    // Java 8 backports
    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:1.0.9'

    // AutoValue Dependencies
    api "com.google.auto.value:auto-value-annotations:1.7.3"
    annotationProcessor "com.google.auto.value:auto-value:1.7.3"

    // Protobuf dependency
    implementation 'com.google.protobuf:protobuf-java:3.11.4'

    // Guava dependency
    api "com.google.guava:guava:29.0-android"

    api "androidx.core:core:1.3.0"
    implementation "androidx.annotation:annotation:1.1.0"

    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'org.robolectric:robolectric:4.3.1'
}